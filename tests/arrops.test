package require tcltest
package require tjson

namespace import -force ::tcltest::test

::tcltest::configure {*}$argv

proc setup {} {
    global node_handle
    set node_handle [::tjson::parse {[1,2,3]}]
}

proc cleanup {} {
    global node_handle
    ::tjson::destroy $node_handle
}

test size-1 {size of a json object} -setup setup -cleanup cleanup -body {
    global node_handle
    ::tjson::size $node_handle
} -result 3

test add_item_to_array-1 {add items of different types to array} -setup setup -cleanup cleanup -body {
    global node_handle
    ::tjson::add_item_to_array $node_handle [list N 4]
    ::tjson::add_item_to_array $node_handle [list L [list {N 5} {N 6} {N 7}]]
    ::tjson::add_item_to_array $node_handle [list M [list a {S "hello world"} b {N 9}]]
    ::tjson::to_simple $node_handle
} -result {1 2 3 4 {5 6 7} {a {hello world} b 9}}

test replace_item_in_array-1 {replace an array element with another} -setup setup -cleanup cleanup -body {
    global node_handle
    ::tjson::replace_item_in_array $node_handle 1 [list N 4]
    ::tjson::to_simple $node_handle
} -result {1 4 3}

test delete_item_from_array-1 {delete an element from an array} -setup setup -cleanup cleanup -body {
    global node_handle
    ::tjson::delete_item_from_array $node_handle 1
    ::tjson::to_simple $node_handle
} -result {1 3}

test get_array_item-1 {get an element from an array} -setup setup -cleanup cleanup -body {
    global node_handle
    set item_handle [::tjson::get_array_item $node_handle 1]
    ::tjson::to_simple $item_handle
} -result {2}

test get_array_item-2-error {index out of bounds} -setup setup -cleanup cleanup -body {
    global node_handle
    set item_handle [::tjson::get_array_item $node_handle 10]
    ::tjson::to_simple $item_handle
} -returnCodes error -result {index out of bounds}

test error_after_deletion-1 {error after deletion} -setup setup -cleanup cleanup -body {
    global node_handle
    set item_handle [::tjson::get_array_item $node_handle 1]
    ::tjson::delete_item_from_array $node_handle 1
    ::tjson::to_simple $item_handle
} -returnCodes error -result {node not found}